buildscript {
    ext {
        kotlinVersion = "1.3.0-rc-146"
        coroutineVersion = "0.30.2-eap13"

        arrowVersion = "0.7.3"

        jUnitVersion = "5.3.1"

        spekVersion = "2.0.0-rc.1"
        ktorVersion = "0.9.5-rc13"
        kluentVersion = "1.4"

        logbackVersion = "1.2.3"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.0-rc-146'
    id "org.jetbrains.kotlin.kapt" version "1.3.0-rc-146"
    id "io.gitlab.arturbosch.detekt"  version "1.0.0.RC8"
}

group 'org.yourcompany'
version '0.1'

apply plugin: "jacoco"
apply plugin: "application"
apply plugin: "idea"

mainClassName = "org.yourcompany.boilerplate.Application"

repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap' }
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion") {
        exclude group: 'org.jetbrains.kotlin'
    }
    compile "io.arrow-kt:arrow-core:$arrowVersion"

    compile "ch.qos.logback:logback-core:$logbackVersion"
    compile "ch.qos.logback:logback-classic:$logbackVersion"

    testCompile "org.jetbrains.kotlin:kotlin-test"

    testCompile "org.amshove.kluent:kluent:$kluentVersion"

    testImplementation "org.junit.jupiter:junit-jupiter-api:$jUnitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jUnitVersion"

    testImplementation ("org.spekframework.spek2:spek-dsl-jvm:$spekVersion")  {
        exclude group: 'org.jetbrains.kotlin'
    }
    testRuntimeOnly ("org.spekframework.spek2:spek-runner-junit5:$spekVersion") {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }
}

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled true
        html.enabled false
        csv.enabled false
    }
}

detekt {
    version = "1.0.0.RC9.2"
    profile("main") {
        input = "$projectDir/src/main/kotlin"
        config = "$rootDir/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

test {
    finalizedBy detektCheck
    finalizedBy jacocoTestReport

    useJUnitPlatform {
        includeEngines 'spek2'
    }

    testLogging {
        events "passed", "skipped", "failed"
    }
}

kotlin {
    experimental {
    }
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
        allWarningsAsErrors = true
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
        allWarningsAsErrors = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true

        sourceDirs += file('build/generated/source/kapt/main')
        testSourceDirs += file('build/generated/source/kapt/test')

        sourceDirs += file('build/generated/source/kaptKotlin/main')
        testSourceDirs += file('build/generated/source/kaptKotlin/test')

        sourceDirs += file('build/tmp/kapt/main/kotlinGenerated')
        testSourceDirs += file('build/tmp/kapt/test/kotlinGeneratedst')
    }
}

task compile(dependsOn: "testClasses")

wrapper {
    gradleVersion = '4.10.2'
}
